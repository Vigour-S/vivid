def vJavaLang = '1.8'
def javaProjects = [ project(':vivid-site'), project(':vivid-common') ]
def bootProjects = javaProjects - project(':vivid-common')
def gradleDir = "${rootProject.rootDir}/gradle"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE')
    }
}

configure(allprojects) {
    apply plugin: 'idea'
}

configure(rootProject) {
    apply from: 'gradle/gitMetadata.gradle'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}


configure(javaProjects) {
    apply plugin: 'java'

    targetCompatibility = vJavaLang
    sourceCompatibility = vJavaLang

    repositories {
        jcenter()
    }

    dependencies {
        compile 'org.slf4j:slf4j-api'
        compile 'org.projectlombok:lombok:1.16.2'
        testCompile 'junit:junit'
        testCompile 'org.hamcrest:hamcrest-library'
        testCompile 'org.mockito:mockito-core'
    }

    configurations {
        // replaced with jcl-over-slf4j
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        // replaced with log4j-over-slf4j
        all*.exclude group: 'log4j', module: 'log4j'
    }
}

configure(bootProjects) {
    apply plugin: 'spring-boot'
    apply from: "$gradleDir/writeGitPropertiesFile.gradle"

    springBoot.backupSource = false
    bootRun.dependsOn writeGitPropertiesFile

    dependencies {
        versionManagement 'io.spring.platform:platform-versions:1.0.5.RELEASE@properties'
    }
}
