import org.gradle.api.tasks.Exec
import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: "java"

jar {
    from "dist"
    eachFile { details ->
        details.path = details.path.startsWith("META-INF") ?: "static/"+details.path
    }
    // Jar contains duplicate empty folders, see Gradle issue:
    // http://issues.gradle.org/browse/GRADLE-1830
    // so we need to set includeEmptyDirs to false
    includeEmptyDirs = false

}

task npmInstall(type:Exec) {

    logging.captureStandardOutput LogLevel.INFO     // be Gradle-like, reduce noisy logging
    logging.captureStandardError LogLevel.LIFECYCLE // but do show downloads in the log

    inputs.files "package.json"
    outputs.files "node_modules", "src/lib"

    if(Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'npm', 'install'
    }
    else {
        commandLine 'npm', 'install'
    }
}

task npmBuild(type:Exec, dependsOn: npmInstall) {

    logging.captureStandardOutput LogLevel.INFO
    logging.captureStandardError LogLevel.LIFECYCLE

    inputs.dir "src"
    inputs.file "gulpfile.js"
    outputs.dir "dist"

    if(Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'npm', 'run', 'build'
    }
    else {
        commandLine 'npm', 'run', 'build'
    }
}

jar.dependsOn npmBuild
